/*

Gradle build scripts are written in Groovy. Groovy is a language that runs on
the JVM that fills a hole for Java developers who need a scripting language.
It's terse, expressive, interoperates extremely well with Java, and has some
special features that make it ideally suited for creating domain specific
languages.

While we don't need to know a ton of Groovy to write Gradle build scripts, it
helps a lot to experiment a bit with the basics. Gradle provides its own
Groovy distribution, so we don't even need to install Groovy to get started.
We can just put our Groovy code in a build.gradle file, and then ask Gradle to
do anything that requires reading the build script.

We'll just declare a task called `groovy` that doesn't do anything. To run
this script, just ask Gradle to run the `groovy` task using

    $ gradle groovy

If you're using Sublime Text, you can turn on Groovy syntax coloring by
selecting View > Syntax > Open all with current extension as... > Groovy

*/

task groovy {}

println "Hello Groovy!"

/*

Most valid Java is also valid Groovy, and we can interoperate with Java, as
well as access the standard library. Let's make a Java class.

*/

class JavaGreeter {
    public void sayHello() {
        System.out.println("Hello Java!");
    }
}

JavaGreeter greeter = new JavaGreeter()
greeter.sayHello()

/*

Groovy is dynamically typed, which means type checking happens at runtime.
That also means you don't need to declare the types of variables.

*/

def foo = 6.5

/*

Groovy makes is super easy to build up strings using string interpolation.
A variable name prefaced with a dollar sign will have the appropriate value
inserted.

*/

println "foo has value: $foo"

/*

We can also surround any Groovy code with a dollar sign and curly braces.

*/

println "Let's do some math. 5 + 6 = ${5 + 6}"

/*

We can reassign a variable to a different type, and we can also inspect the
type of a variable using `foo.class`.

*/

println "foo is of type: ${foo.class} and has value: $foo"
foo = "a string"
println "foo is now of type: ${foo.class} and has value: $foo"

/*

The `println` keyword is just a shortcut for `System.out.println`, but you've
probably noticed that we're not using any parentheses, and there's not a
semicolon in sight. The latter are always optional, and parentheses can be
omitted whenever a function is called with at least one argument, and the
assignment of arguments to function calls is unambiguous.

These syntactic features are part of what make Groovy such a good choice for a
domain specific language. Let's define a function.

*/

def doubleIt(n) {
    n + n // Note we don't need a return statement
}

/*

Note that we don't need to specify the type of the arguments, nor the return
type. We don't even need to supply a return statement. The last expression in
the block is returned. Let's give it a try.

*/

foo = 5
println "doubleIt($foo) = ${doubleIt(foo)}"

/*

Even more interestingly, since the plus sign is overloaded to concatenate
strings, we can call our function with a string as well.

*/

foo = "foobar"
println "doubleIt($foo) = ${doubleIt(foo)}"

/*

Let's define a few functions to show off one of the coolest syntactic features
of Groovy; you don't need to use parentheses to call a function with at least
one argument.

*/

def noArgs() {
    println "Called the no args function"
}

def oneArg(x) {
    println "Called the 1 arg function with $x"
    x
}

def twoArgs(x, y) {
    println "Called the 2 arg function with $x and $y"
    x + y
}

oneArg 500 // Look, Ma! No parentheses!
twoArgs 200, 300
noArgs()
//noArgs // Doesn't work
//twoArgs oneArg 500, 200 // Also doesn't work as it's ambiguous
twoArgs oneArg(500), 200 // Fixed the ambiguity
